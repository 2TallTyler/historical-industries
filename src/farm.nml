// Begin Farm

spritelayout sprlay_farm_1_N { // Farmhouse N
	ground		{sprite:2106;}
	building	{sprite:2108;}
}

spritelayout sprlay_farm_1_E { // Farmhouse W
	ground		{sprite:2107;}
	building	{sprite:2109;}
}

switch (FEAT_OBJECTS, SELF, switch_object_graphics_farm_1, relative_pos) {
	relative_coord(0, 0): sprlay_farm_1_N;
	relative_coord(0, 1): sprlay_farm_1_E;
}

spritelayout sprlay_farm_2 { // Hay shed
	ground		{sprite:2110;}
	building	{sprite:2111;}
}

spritelayout sprlay_farm_3 { // Barn
	ground		{sprite:2112;}
	building	{sprite:2113;}
}

spritelayout sprlay_farm_4 { // Silos
	ground		{sprite:2116;}
	building	{sprite:2115;}
}

spritelayout sprlay_farm_5 { // Farmyard
	ground		{sprite:2116;}
	building	{sprite:2117;}
}

spritelayout sprlay_farm_6 { // Empty tile
	ground		{sprite:2116;}
}

/* Autoslope Fields */

spritelayout sprlay_farm_field_1 {
 	ground {
 		// normal ground sprite - always draw
 		sprite: 4126 + LOAD_TEMP(0);
 	}
 }

switch (FEAT_OBJECTS, SELF, switch_farm_field_1_autoslope, [
 	STORE_TEMP(slope_to_sprite_offset(tile_slope), 0),
	]) {
		sprlay_farm_field_1;
 }

item (FEAT_INDUSTRYTILES, ind_tile_farm_1_N) { // House north
property {substitute:	02;}
graphics {default:		sprlay_farm_1_N;}}

item (FEAT_INDUSTRYTILES, ind_tile_farm_1_E) { // House east
property {substitute:	02;}
graphics {default:		sprlay_farm_1_E;}}
 
item (FEAT_INDUSTRYTILES, ind_tile_farm_2) { // Hay shed
property {substitute:	02;}
graphics {default:		sprlay_farm_2;}}

item (FEAT_INDUSTRYTILES, ind_tile_farm_3) { // Barn
property {substitute:	02;}
graphics {default:		sprlay_farm_3;}}

item (FEAT_INDUSTRYTILES, ind_tile_farm_4) { // Silos
property {substitute:	02;}
graphics {default:		sprlay_farm_4;}}

item (FEAT_INDUSTRYTILES, ind_tile_farm_5) { // Farmyard
property {substitute:	02;}
graphics {default:		sprlay_farm_5;}}

item (FEAT_INDUSTRYTILES, ind_tile_farm_6) { // empty tile
property {substitute:	02;}
graphics {default:		sprlay_farm_6;}}

tilelayout industry_layout_farm_1 {
	0,0:	ind_tile_farm_4;	0,1:	ind_tile_farm_3;	0,2:	ind_tile_farm_2;
	1,0:	ind_tile_farm_1_N;	1,1:	ind_tile_farm_1_E;	1,2:	ind_tile_farm_6;
}
tilelayout industry_layout_farm_2 {
	0,0:	ind_tile_farm_5;	0,1:	ind_tile_farm_4;	0,2:	ind_tile_farm_2;
	1,0:	ind_tile_farm_3;	1,1:	ind_tile_farm_1_N;	1,2:	ind_tile_farm_1_E;
}
tilelayout industry_layout_farm_3 {
	0,0:	ind_tile_farm_5;	0,1:	ind_tile_farm_1_N;	0,2:	ind_tile_farm_1_E;
	1,0:	ind_tile_farm_5;	1,1:	ind_tile_farm_3;	1,2:	ind_tile_farm_4;
}
tilelayout industry_layout_farm_4 {
	0,0:	ind_tile_farm_1_N;	0,1:	ind_tile_farm_1_E;	0,2:	ind_tile_farm_2;
	1,0:	ind_tile_farm_3;	1,1:	ind_tile_farm_6;	1,2:	ind_tile_farm_5;
}
tilelayout industry_layout_farm_5 {
	0,0:	ind_tile_farm_1_N;	0,1:	ind_tile_farm_1_E;	0,2:	ind_tile_farm_4;
	1,0:	ind_tile_farm_3;	1,1:	ind_tile_farm_6;	1,2:	ind_tile_farm_2;
}
tilelayout industry_layout_farm_6 {
	0,0:	ind_tile_farm_1_N;	0,1:	ind_tile_farm_1_E;	0,2:	ind_tile_farm_5;
	1,0:	ind_tile_farm_2;	1,1:	ind_tile_farm_6;	1,2:	ind_tile_farm_5;
}
tilelayout industry_layout_farm_7 {
	0,0:	ind_tile_farm_3;	0,1:	ind_tile_farm_6;	0,2:	ind_tile_farm_5;
	1,0:	ind_tile_farm_1_N;	1,1:	ind_tile_farm_1_E;	1,2:	ind_tile_farm_2;
}



switch (FEAT_INDUSTRIES, SELF, switch_farm_location_check_5, industry_distance(industry_farm,-1) >= 40 && industry_distance(industry_steel_mill,-1) >= 30 && industry_distance(industry_oil_refinery,-1) >= 30 && industry_distance(industry_paper_mill,-1) >= 30 && industry_distance(industry_factory,-1) >= 30 && industry_distance(industry_iron_mine,-1) >= 30 && industry_distance(industry_coal_mine,-1) >= 30 && industry_distance(industry_oil_wells,-1) >= 30 && industry_distance(industry_logging_camp,-1) >= 30) {1: CB_RESULT_LOCATION_ALLOW; CB_RESULT_LOCATION_DISALLOW;} // Don't allow construction within 60 tiles (Manhattan) of steel mill
switch (FEAT_INDUSTRIES, PARENT, switch_farm_location_check_4, is_city) {1: return CB_RESULT_LOCATION_DISALLOW; switch_farm_location_check_5;} // don't build in cities
switch (FEAT_INDUSTRIES, SELF, switch_farm_location_check_3, town_euclidean_dist(0,0) >= LOAD_TEMP(0)+100) {1: switch_farm_location_check_4; return CB_RESULT_LOCATION_DISALLOW;} // Add 10 to the town zone radii and check if the distance to the town sign is larger than that radius. The town zone radii and distance check use the square of the distance, thus 64 = 8 tiles. 
switch (FEAT_INDUSTRIES, PARENT, switch_farm_location_check_2, STORE_TEMP(town_zone_0_radius_square + town_zone_1_radius_square + town_zone_2_radius_square + town_zone_3_radius_square + town_zone_4_radius_square, 0)) {switch_farm_location_check_3;} // Add up the town zone radii and store it in temporary register 0
switch (FEAT_INDUSTRIES, SELF, switch_farm_location_check_1, nearby_tile_height(0,0) > 2) {1: return CB_RESULT_LOCATION_DISALLOW; switch_farm_location_check_2;} // Farms must be built at tile height 2 or below.


/*	Slot Reservations - all industries */

// Compare how many industries are currently on the map with the target number, scaled to the map size from a "normal" 256 x 256 map. If less than the target number, proceed to town distance checks, otherwise disallow construction.
switch (FEAT_INDUSTRIES, SELF, switch_farm_density_high,
	industry_count(industry_farm, -1) < 12 * (map_x_edge / 256) * (map_y_edge / 256)) 
	{1: switch_farm_location_check_1; return CB_RESULT_LOCATION_DISALLOW;}

switch (FEAT_INDUSTRIES, SELF, switch_farm_density_normal,
	industry_count(industry_farm, -1) < 12 * (map_x_edge / 256) * (map_y_edge / 256)) 
	{1: switch_farm_location_check_1; return CB_RESULT_LOCATION_DISALLOW;}

switch (FEAT_INDUSTRIES, SELF, switch_farm_density_low,
	industry_count(industry_farm, -1) < 5 * (map_x_edge / 256) * (map_y_edge / 256)) 
	{1: switch_farm_location_check_1; return CB_RESULT_LOCATION_DISALLOW;}

switch (FEAT_INDUSTRIES, SELF, switch_farm_density_verylow,
	industry_count(industry_farm, -1) < 1 * (map_x_edge / 256) * (map_y_edge / 256)) 
	{1: switch_farm_location_check_1; return CB_RESULT_LOCATION_DISALLOW;}

// Get the selected industry density from parameter and redirect to the appropriate switch
switch (FEAT_INDUSTRIES, SELF, switch_farm_density_selector, param_industry_density) {
	0: switch_farm_density_verylow;
	1: switch_farm_density_low;
	2: switch_farm_density_normal;
	3: switch_farm_density_high;
}

/*
Industry Totals on 256 x 256 map:

	Industry		Very Low (10)	Low (25)	Normal (55)		High (80)
	Oil Wells		1				4			8				12
	Farm			1				5			12				12
	Lumber Camp		1				3			8				12
	Coal Mine		1				3			6				12
	Iron Mine		1				3			6				12
	Steel Mill		1				1			2				3
	Factory			1				3			6				7
	Paper Mill		1				1			2				3
	Oil Refinery	1				1			2				3
	Power Plant		1				1			3				4
*/
	
// Funded industries aren't restricted by industry totals
switch (FEAT_INDUSTRIES, SELF, switch_farm_allow_funded, extra_callback_info2 == IND_CREATION_FUND) 
	{1: return 1; switch_farm_density_selector;}

item (FEAT_INDUSTRIES, industry_farm) {
	property {
		substitute:				INDUSTRYTYPE_TEMPERATE_ARCTIC_FARM;
		life_type:				IND_LIFE_TYPE_ORGANIC;
		spec_flags:				bitmask(IND_FLAG_PLANT_FIELDS_WHEN_BUILT, IND_FLAG_PLANT_FIELDS_PERIODICALLY);
		cargo_types:			[
								produce_cargo("FOOD",10),
		];
		name:					string(STR_NAME_FARM);
		nearby_station_name:	string(STR_STATION, string(STR_TOWN), string(STR_NAME_FARM));
		prob_map_gen:			30;
		prob_in_game:			20;
		fund_cost_multiplier:	100;
		layouts:				[
			industry_layout_farm_1,
			industry_layout_farm_2,
			industry_layout_farm_3,
			industry_layout_farm_4,
			industry_layout_farm_5,
			industry_layout_farm_6,
			industry_layout_farm_7,
		];
	}
	graphics {
		location_check:			switch_farm_allow_funded;
		construction_probability:	1;
	}
} 
 
 
 

item (FEAT_OBJECTS, object_item_farm_1) {
	property {
		class:					"HSIN";
		classname:				string(STR_NAME_OBJECT_MENU);
		name:					string(STR_NAME_FARM);
		climates_available:		ALL_CLIMATES;
		object_flags:			bitmask(OBJ_FLAG_ANYTHING_REMOVE);
		build_cost_multiplier:	0;
		remove_cost_multiplier:	0;
		size:					[1,2];
		introduction_date:		1700;
		num_views:				1;
	}
	graphics {
		default:				switch_object_graphics_farm_1;
	}
}

item (FEAT_OBJECTS, object_item_farm_2) {
	property {
		class:					"HSIN";
		classname:				string(STR_NAME_OBJECT_MENU);
		name:					string(STR_NAME_FARM);
		climates_available:		ALL_CLIMATES;
		object_flags:			bitmask(OBJ_FLAG_ANYTHING_REMOVE);
		build_cost_multiplier:	0;
		remove_cost_multiplier:	0;
		size:					[1,1];
		introduction_date:		1700;
		num_views:				1;
	}
	graphics {
		default:				sprlay_farm_2;
	}
}

item (FEAT_OBJECTS, object_item_farm_3) {
	property {
		class:					"HSIN";
		classname:				string(STR_NAME_OBJECT_MENU);
		name:					string(STR_NAME_FARM);
		climates_available:		ALL_CLIMATES;
		object_flags:			bitmask(OBJ_FLAG_ANYTHING_REMOVE);
		build_cost_multiplier:	0;
		remove_cost_multiplier:	0;
		size:					[1,1];
		introduction_date:		1700;
		num_views:				1;
	}
	graphics {
		default:				sprlay_farm_3;
	}
}

item (FEAT_OBJECTS, object_item_farm_4) {
	property {
		class:					"HSIN";
		classname:				string(STR_NAME_OBJECT_MENU);
		name:					string(STR_NAME_FARM);
		climates_available:		ALL_CLIMATES;
		object_flags:			bitmask(OBJ_FLAG_ANYTHING_REMOVE);
		build_cost_multiplier:	0;
		remove_cost_multiplier:	0;
		size:					[1,1];
		introduction_date:		1700;
		num_views:				1;
	}
	graphics {
		default:				sprlay_farm_4;
	}
}

item (FEAT_OBJECTS, object_item_farm_5) {
	property {
		class:					"HSIN";
		classname:				string(STR_NAME_OBJECT_MENU);
		name:					string(STR_NAME_FARM);
		climates_available:		ALL_CLIMATES;
		object_flags:			bitmask(OBJ_FLAG_ANYTHING_REMOVE);
		build_cost_multiplier:	0;
		remove_cost_multiplier:	0;
		size:					[1,1];
		introduction_date:		1700;
		num_views:				1;
	}
	graphics {
		default:				sprlay_farm_5;
	}
}

item (FEAT_OBJECTS, object_item_farm_6) {
	property {
		class:					"HSIN";
		classname:				string(STR_NAME_OBJECT_MENU);
		name:					string(STR_NAME_FARM);
		climates_available:		ALL_CLIMATES;
		object_flags:			bitmask(OBJ_FLAG_ANYTHING_REMOVE);
		build_cost_multiplier:	0;
		remove_cost_multiplier:	0;
		size:					[1,1];
		introduction_date:		1700;
		num_views:				1;
	}
	graphics {
		default:				sprlay_farm_6;
	}
}

item (FEAT_OBJECTS, object_item_farm_field_1) {
	property {
		class:					"HSIN";
		classname:				string(STR_NAME_OBJECT_MENU);
		name:					string(STR_NAME_FIELD_1);
		climates_available:		ALL_CLIMATES;
		object_flags:			bitmask(OBJ_FLAG_ANYTHING_REMOVE, OBJ_FLAG_NO_FOUNDATIONS);
		build_cost_multiplier:	0;
		remove_cost_multiplier:	0;
		size:					[1,1];
		introduction_date:		1700;
		num_views:				1;
	}
	graphics {
		default:				switch_farm_field_1_autoslope;
		purchase:				sprlay_farm_field_1;
	}
}

// End Farm